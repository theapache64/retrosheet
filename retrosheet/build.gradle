plugins {
    id 'java'
    id 'org.jetbrains.kotlin.jvm'
    id 'maven'
    id 'maven-publish'
    id 'com.jfrog.bintray' version '1.8.5'
    id "org.jetbrains.kotlin.kapt"
}
apply from: "${rootProject.projectDir}/dependencies.gradle"

group 'com.theapache64'
version "$versions.retrosheet_version"

repositories {
    jcenter()
    mavenCentral()
}

task sourcesJar(type: Jar, dependsOn: classes) {
    archiveClassifier.set('sources')
    from sourceSets.main.allSource
}

javadoc.failOnError = false
task javadocJar(type: Jar, dependsOn: javadoc) {
    archiveClassifier.set('javadoc')
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

def pomConfig = {
    licenses {
        license {
            name "The Apache Software License, Version 2.0"
            url "http://www.apache.org/licenses/LICENSE-2.0.txt"
            distribution "repo"
        }
    }
    developers {
        developer {
            id "shifarshifz"
            name "shifarshifz"
            email "theapache64@gmail.com"
        }
    }

    scm {
        url "https://github.com/theapache64/retrosheet"
    }
}

publishing {
    publications {
        mavenPublication(MavenPublication) {
            from components.java
            artifact sourcesJar {
                classifier "sources"
            }
            artifact javadocJar {
                classifier "javadoc"
            }
            groupId 'com.theapache64'
            artifactId 'retrosheet'
            version "$versions.retrosheet_version"
            pom.withXml {
                def root = asNode()
                root.appendNode('description', 'Turn Google Spreadsheet to JSON endpoint (for Android and JVM)')
                root.appendNode('name', 'retrosheet')
                root.appendNode('url', 'https://github.com/theapache64/retrosheet')
                root.children().last() + pomConfig
            }
        }
    }
}


dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
    testCompile group: 'junit', name: 'junit', version: '4.12'

    // FastCSV : Ultra fast and simple RFC 4180 compliant CSV library.
    implementation 'de.siegmar:fastcsv:1.0.3'

    // license includes this restriction: "The software ...
    implementation 'org.json:json:20200518'

    // Retrofit : A type-safe HTTP client for Android and Java.
    implementation 'com.squareup.retrofit2:retrofit:2.9.0'

    // Moshi : Moshi
    implementation 'com.squareup.moshi:moshi:1.9.3'

    // Moshi Kotlin Codegen : Moshi Kotlin Codegen
    kapt 'com.squareup.moshi:moshi-kotlin-codegen:1.9.3'

    // Expekt : An assertion library for Kotlin
    testImplementation 'com.theapache64:expekt:0.0.1'
}

compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
}
compileTestKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

bintray {
    user = System.getenv('BINTRAY_USER')
    key = System.getenv('BINTRAY_API_KEY')
    publications = ['mavenPublication']

    pkg {
        repo = 'maven'
        name = 'retrosheet'
        userOrg = 'shifarshifz'
        licenses = ['Apache-2.0']
        vcsUrl = 'https://github.com/theapache64/retrosheet.git'
        version {
            name = versions.retrosheet_version
            desc = versions.retrosheet_version
            released = new Date()
        }
    }

}